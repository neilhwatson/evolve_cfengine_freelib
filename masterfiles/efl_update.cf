# This is a work in progress and it not well tested.

# This file is alternative update file. It is simpler, yet reduces server load
# and uses server reduncancy.

# namespace left out due to bug https://cfengine.com/dev/issues/4762
# body file control
# {
#    namespace => "eflu";
# }

body common control
{
   bundlesequence => { "main" };
}

bundle agent config
{
   meta:
      "purpose" string => "User defined configs here.";

   defaults:
      "policy_servers"
         comment        => "Default to bootstrap server",
         slist          => { "${sys.policy_hub}" },
         if_match_regex => "";

   vars:
      "modules"
         comment => "Modules required for a minimum working system.",
         slist   => { "return_index.pl", "cf-manifest" };

      "modules_path"
         comment => "Where to find modules on the policy server.",
         string  => "${sys.workdir}/modules";

      "inputs_manifest"
         comment => "Manifest file to determine what inputs to download",
         string  => "efl_manifest.txt";

      "cfengine_procs"
         comment => "CFEngine processes to start or restart",
         slist   => { "cf-serverd", "cf-execd", "cf-monitord" };

      "comment"
         comment => "Comment string in data file.",
         string  => "\s*#[^\n]*";

      "array_delimiter"
         comment => "Field delimiter for CSV data files read by readstringarrayidx",
         string  => "\s*;;\s*";

      "max_num"
         comment => "Maximum number of lines to read from data file",
         int     => "500";

      "max_bytes"
         comment => "Maximum number of bytes to read from data file.",
         string  => "1M";

      "policy_servers_file"
         comment => "File that contains redundant policy servers.",
         string  => "${sys.workdir}/efl_policy_servers.dat";

      load_policy_servers::
         "policy_servers_unshuffled"
            comment => "Read into string for processing",
            slist   => readstringlist(
               "${policy_servers_file}",
               "#[^\n]*", # Allowed comments in file.
               "[\n]",    # One server IP or hostname per line.
               "10",      # Maximum allowed entries.
               "10K"      # Maximum file size
               );

         "seed"
            comment => "Seed deterministic shuffle.",
            string  => "${sys.fqhost}";

         "policy_servers"
            comment => "Shuffle deterministically for load balancing",
            slist   => shuffle( "policy_servers_unshuffled", "${seed}" );

   classes:
      "load_policy_servers"
         comment    => "Not to be confused with policy_server.dat",
         expression => fileexists( "${policy_servers_file}" );

      "daily_inputs_sync_with_purge"
         scope      => "namespace",
         expression => splayclass( "${sys.host}", "daily" );

   reports:
      debug::
         "policy_servers => ${policy_servers}"
            comment => "Set debug to see debugging reports";
}

bundle agent main
{
   meta:
      "purpose" string => "Drives bundle order through method calls.";

   methods:
      "Load configs"
         handle => "main_methods_config",
         usebundle => config;

      !daily_inputs_sync_with_purge::
         "Download manifest"
            handle     => "main_methods_download_manifest",
            depends_on => { "main_methods_config" },
            usebundle  => download_manifest ( "${config.inputs_manifest}" );

         "Update inputs and modules"
            handle     => "main_methods_inputs",
            depends_on => { "main_methods_config", "main_methods_download_manifest" },
            usebundle  => inputs_and_modules ( "${sys.workdir}/inputs/${config.inputs_manifest}" );

      daily_inputs_sync_with_purge::
         "Recurse inputs copy with purge"
            handle     => "main_methods_inputs_purge",
            depends_on => { "main_methods_config" },
            usebundle  => inputs_purge;

      any::
         "Start processes"
            handle     => "main_methods_start_procs",
            depends_on => { "main_methods_config" },
            usebundle  => start_procs;
}

bundle agent download_manifest( ref )
{
   meta:
      "purpose" string => "Download inputs manifest file";

   files:
      "${sys.workdir}/inputs/${ref}"
         handle    => "download_manifest_files_inputs_manifest",
         perms     => mog( "600", "root", "root" ),
         copy_from => secure_cp( 
            "${sys.workdir}/masterfiles/${ref}",
            # "@{eflu:config.policy_servers}" # prefix with namespace because of bug
            "@{config.policy_servers}" 
            );
}

bundle agent inputs_purge
{
   meta:
      "purpose" string => "Do a full sync of inputs with purging on.";

   files:
      "${sys.workdir}/inputs/"
         handle       => "inputs_purge_files_inputs",
         perms        => mog( "600", "root", "root" ),
         classes      => if_repaired( "kill_cf_procs" ),
         depth_search => recurse( "inf" ),
         copy_from    => secure_sync( "${sys.workdir}/masterfiles/", "@{config.policy_servers}" );

   processes:
      kill_cf_procs::
         "${config.cfengine_procs}"
            handle         => "inputs_purge_processes_kill",
            comment        => "Kill procs for later restart.",
            process_select => by_name( "(${sys.workdir}/bin/){0,1}${config.cfengine_procs}" ),
            signals        => { "term", "kill" };
}

bundle agent inputs_and_modules( ref )
{
   meta:
      "purpose" string => "Download inputs determined by manifest file.";

   vars:
      "dim_manifest"
         comment => "Read data file for parsing.",
         int     => readstringarrayidx(
            "manifest_o",
            "${ref}",
            "${config.comment}",
            "${config.array_delimiter}",
            "${config.max_num}",
            "${config.max_bytes}"
         );

      "r"
         comment => "Get index array loaded above.",
         slist   => getindices( "manifest_o" );

      "r2"
         comment => "Work around for bug 2333",
         slist   => { "0", "1" };
 
      "manifest[${r}][${r2}]"
         comment => "Work around for bug 2333",
         string  => "${manifest_o[${r}][${r2}]}";

      "manifest_file[${r}]"
         comment => "Turn into reference for simpler reading.",
         string  => "manifest[${r}][0]";

      "manifest_md5sum[${r}]"
         comment => "Turn into reference for simpler reading.",
         string  => "manifest[${r}][1]";

   classes:
      "download_${${manifest_file[${r}]}}"
         comment => "Set download class if file is missing.",
         not     => fileexists( "${sys.workdir}/inputs/${${manifest_file[${r}]}}" );

      "download_${${manifest_file[${r}]}}"
         comment    => "Set download class if file checksum does not match manifest.",
         ifvarclass => not( canonify( "download_${${manifest_file[${r}]}}" ) ),
         not        => hashmatch( "${sys.workdir}/inputs/${${manifest_file[${r}]}}", "md5", "${${manifest_md5sum[${r}]}}" );

   files:
      "${sys.workdir}/inputs/${${manifest_file[${r}]}}"
         handle     => "inputs_and_modules_files_inputs_download",
         comment    => "Download file shown to be new by the mainfest",
         ifvarclass => canonify( "download_${${manifest_file[${r}]}}" ),
         perms      => mog( "600", "root", "root" ),
         classes    => if_repaired( "kill_cf_procs" ),
         copy_from  => secure_cp( "${sys.workdir}/masterfiles/${${manifest_file[${r}]}}", "@{config.policy_servers}" );
      
      "${sys.workdir}/inputs/${${manifest_file[${r}]}}"
         handle     => "inputs_and_modules_files_inputs_permissions",
         comment    => "Promise file perms for files not downloaded",
         ifvarclass => not( canonify( "download_${${manifest_file[${r}]}}" ) ),
         perms      => mog( "600", "root", "root" );

# TODO were to put masterfiles module? sitefiles?
      "${sys.workdir}/modules/${config.modules}"
         handle    => "inputs_and_modules_files_modules_download",
         comment   => "Update modules",
         perms     => mog( "700", "root", "root" ),
         copy_from => secure_cp( "${config.modules_path}/${config.modules}", "@{config.policy_servers}" );

   processes:
      kill_cf_procs::
         "${config.cfengine_procs}"
            handle         => "inputs_and_modules_processes_kill",
            comment        => "Kill procs for later restart.",
            process_select => by_name( "(${sys.workdir}/bin/){0,1}${config.cfengine_procs}" ),
            signals        => { "term", "kill" };

   reports:
      debug::
         "download ${${manifest_file[${r}]}}"
            ifvarclass => canonify( "download_${${manifest_file[${r}]}}" );
}

bundle agent start_procs
{
   meta:
      "purpose" string => "Start CFEngine processes";

   processes:
      "${config.cfengine_procs}"
         handle         => "start_procs_processes_start",
         comment        => "Kill procs for later restart.",
         process_select => by_name( "(${sys.workdir}/bin/){0,1}${config.cfengine_procs}" ),
         restart_class  => canonify( "restart_${config.cfengine_procs}" );

   commands:
      "${sys.workdir}/bin/${config.cfengine_procs}"
         handle     => "start_procs_commands_start",
         comment    => "Issue restart commmand",
         ifvarclass => canonify( "restart_${config.cfengine_procs}" );
}
   
body process_select by_name( command_regex )
{
   command        => "${command_regex}";
   process_result => "command";
}

body perms mog(mode,user,group)
{
   owners => { "$(user)" };
   groups => { "$(group)" };
   mode   => "$(mode)";
}

body copy_from secure_cp(from,server)
{
   source      => "$(from)";
   servers     => { "$(server)" };
   compare     => "digest";
   encrypt     => "true";
   verify      => "true";
}

body copy_from secure_sync(from,server)
{
   source      => "$(from)";
   servers     => { "$(server)" };
   compare     => "digest";
   purge       => "true";
   type_check  => "false";
   encrypt     => "true";
   verify      => "true";
}

body classes if_repaired(x)
{
   promise_repaired => { "$(x)" };
}

body depth_search recurse(d)
{
depth => "$(d)";
xdev  => "true";
}
